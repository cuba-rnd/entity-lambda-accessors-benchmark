import java.nio.file.Files

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
}

sourceSets {
    jmh
}

group 'com.haulmont.cuba.testperformance'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.7'
    jmhCompile project
    jmhCompile 'org.openjdk.jmh:jmh-core:1.21'
    jmhCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.21'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

def reportFile = new File("$buildDir/reports/jmh/result.json").toPath()


task recreateReportFile { //Touch from JMH framework fails on Windows

    doLast {
        Files.deleteIfExists(reportFile)
        Files.createDirectories(reportFile.getParent())
        Files.createFile(reportFile)
    }

}

task jmh(type: JavaExec, description: 'Executing JMH benchmarks', dependsOn: 'recreateReportFile') {

    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.openjdk.jmh.Main'

    args '-rff', reportFile.toFile(), '-rf', 'json'
}
